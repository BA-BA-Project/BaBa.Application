plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
}

def properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    namespace 'kids.baba.mobile'
    compileSdk 33

    defaultConfig {
        applicationId "kids.baba.mobile"
        minSdk 28
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    flavorDimensions "app"

    productFlavors {
        dev {
            dimension "app"
            manifestPlaceholders = [
                    appLabel: "BABA-dev",
                    KAKAO_NATIVE_APP_KEY: properties.getProperty("kakao.native.app.key")
            ]
            buildConfigField "String", "BASE_URL", "\"https://dev.baba.kids/api/\""
            buildConfigField "String", "KAKAO_NATIVE_APP_KEY", "\"${properties.getProperty("kakao.native.app.key")}\""
        }
        product {
            dimension "app"
            manifestPlaceholders = [
                    appLabel: "BABA",
                    KAKAO_NATIVE_APP_KEY: properties.getProperty("kakao.native.app.key")
            ]
            buildConfigField "String", "BASE_URL", "\"https://baba.kids/api/\""
            buildConfigField "String", "KAKAO_NATIVE_APP_KEY", "\"${properties.getProperty("kakao.native.app.key")}\""
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation "com.kakao.sdk:v2-share:2.13.0"

    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.fragment:fragment-ktx:1.5.5'

    implementation 'com.kizitonwose.calendar:view:2.2.0'

    // 정식 버전인 security-crypto:1.0.0을 사용하면 좋겠으나....
    // 의미 없는 스택 트레이스가 눈에 거슬리게 뜨는 관계로 아래 라이브러리 이용
    // 추후 앱 출시할 때도 아래 라이브러리의 정식 버전이 안 나오면 security-crypto:1.0.0 으로 대체
    // TODO: 앱 출시전 아래 라이브러리 검토 필요
    implementation "androidx.security:security-crypto-ktx:1.1.0-alpha05"

    // hilt
    def hiltVersion = "2.44"
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"

    // retrofit2
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // okHttp3
    def okHttpVersion = "4.10.0"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // splash
    def splashVersion = "1.0.0"
    implementation "androidx.core:core-splashscreen:$splashVersion"

    // viewPager
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "me.relex:circleindicator:2.1.6"

    // kakao
    implementation "com.kakao.sdk:v2-user:2.12.1" // 카카오 로그인
    implementation "com.kakao.sdk:v2-talk:2.13.0" // 친구, 메시지(카카오톡)
    implementation "com.kakao.sdk:v2-share:2.13.0" // 메시지(카카오톡 공유)
    implementation "com.kakao.sdk:v2-friend:2.13.0" // 카카오톡 소셜 피커, 리소스 번들 파일 포함


    // navigation
    def navVersion = "2.5.3"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"


    // cameraX
    def camerax_version = "1.3.0-alpha04"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-video:${camerax_version}"

    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"


    // crop
    implementation("com.vanniktech:android-image-cropper:4.5.0")

    // Exifinterface
    implementation "androidx.exifinterface:exifinterface:1.3.6"

    // selection
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0"

    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    //Glide
    def glideVersion = "4.15.0"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    //Lottie
    def lottieVersion = "6.0.0"
    implementation "com.airbnb.android:lottie:$lottieVersion"
}

kapt {
    correctErrorTypes true
}